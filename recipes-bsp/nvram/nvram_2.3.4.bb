DESCRIPTION = "NVRAM base program"

LICENSE = "MIT"
LIC_FILES_CHKSUM = "file://LICENSE;md5=14e5f6d6fc625ef5ece406e9c85a768a"

SRCREV ?= "293d7ae4acaf998ed485b081bdbdd3f877f113b7"
SRC_URI = "gitsm://git@github.com/akkodis-edge/nvram.git;protocol=https;branch=master"

S = "${WORKDIR}/git"

NVRAM_SYSTEM_PREFIX ??= "SYS_"
NVRAM_INTERFACE_DEFAULT ??= "file"
NVRAM_FORMAT_DEFAULT ??= "v2"
NVRAM_FILE_SYSTEM_A ??= "/var/nvram/system_a"
NVRAM_FILE_SYSTEM_B ??= "/var/nvram/system_b"
NVRAM_FILE_USER_A ??= "/var/nvram/user_a"
NVRAM_FILE_USER_B ??= "/var/nvram/user_b"
NVRAM_MTD_SYSTEM_A ??= "system_a"
NVRAM_MTD_SYSTEM_B ??= "system_b"
NVRAM_MTD_USER_A ??= "user_a"
NVRAM_MTD_USER_B ??= "user_b"
NVRAM_EFI_SYSTEM_A ??= "/sys/firmware/efi/efivars/NvramSystem-604dafe4-587a-47f6-8604-3d33eb83da3d"
NVRAM_EFI_SYSTEM_B ??= ""
NVRAM_EFI_USER_A ??= "/sys/firmware/efi/efivars/NvramUser-604dafe4-587a-47f6-8604-3d33eb83da3d"
NVRAM_EFI_USER_B ??= ""

FILE_FLAGS = " \
	NVRAM_FILE_SYSTEM_A=${NVRAM_FILE_SYSTEM_A} \
	NVRAM_FILE_SYSTEM_B=${NVRAM_FILE_SYSTEM_B} \
	NVRAM_FILE_USER_A=${NVRAM_FILE_USER_A} \
	NVRAM_FILE_USER_B=${NVRAM_FILE_USER_B} \
"
MTD_FLAGS = " \
	NVRAM_MTD_SYSTEM_A=${NVRAM_MTD_SYSTEM_A} \
	NVRAM_MTD_SYSTEM_B=${NVRAM_MTD_SYSTEM_B} \
	NVRAM_MTD_USER_A=${NVRAM_MTD_USER_A} \
	NVRAM_MTD_USER_B=${NVRAM_MTD_USER_B} \
"
EFI_FLAGS = " \
	NVRAM_EFI_SYSTEM_A=${NVRAM_EFI_SYSTEM_A} \
	NVRAM_EFI_SYSTEM_B=${NVRAM_EFI_SYSTEM_B} \
	NVRAM_EFI_USER_A=${NVRAM_EFI_USER_A} \
	NVRAM_EFI_USER_B=${NVRAM_EFI_USER_B} \
"

PACKAGECONFIG ??= "interface_file format_v2"
PACKAGECONFIG[interface_file] = "NVRAM_INTERFACE_FILE=1 ${FILE_FLAGS},NVRAM_INTERFACE_FILE=0,,"
PACKAGECONFIG[interface_mtd] = "NVRAM_INTERFACE_MTD=1 ${MTD_FLAGS},NVRAM_INTERFACE_MTD=0,mtd-utils,"
PACKAGECONFIG[interface_efi] = "NVRAM_INTERFACE_EFI=1 ${EFI_FLAGS},NVRAM_INTERFACE_EFI=0,e2fsprogs,"
PACKAGECONFIG[format_v2] = "NVRAM_FORMAT_V2=1,NVRAM_FORMAT_V2=0,,"
PACKAGECONFIG[format_legacy] = "NVRAM_FORMAT_LEGACY=1,NVRAM_FORMAT_LEGACY=0,,"
PACKAGECONFIG[format_platform] = "NVRAM_FORMAT_PLATFORM=1,NVRAM_FORMAT_PLATFORM=0,zlib,"

EXTRA_OEMAKE += "\
	NVRAM_INTERFACE_DEFAULT=${NVRAM_INTERFACE_DEFAULT} \
	NVRAM_FORMAT_DEFAULT=${NVRAM_FORMAT_DEFAULT} \
	NVRAM_SYSTEM_PREFIX=${NVRAM_SYSTEM_PREFIX} \
	${PACKAGECONFIG_CONFARGS} \
"

do_install () {
    install -d ${D}${bindir}
    install -m 0755 ${S}/build/nvram ${D}${bindir}
}

FILES:${PN} = "${bindir}/nvram"

PACKAGE_ARCH = "${MACHINE_ARCH}"
